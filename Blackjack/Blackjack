/*
Tecnológico de Costa Rica, Escuela de Computación, Ingeniería en Computación
Curso de Simulación, I Semestre 2021
Tarea de Blackjack
Joseph Tenorio Pereira (2019064588) y Alejandro García Bejarano (2019380047) 

Probabilidad de victoria del jugador obtenida en la simulación: 0.43
*/

/*-----------------------------------------Código Fuente de la simulación-----------------------------------------*/

/* VARIABLES GLOBALES */
GLOBAL deck

/* SUMA DE LAS CARTAS */
newcmd contar cartas suma
   copy cartas naipes
   recode naipes between 10 13 10 naipes
   sum naipes sumaTotal
   
   if (1 memberof naipes) and (sumaTotal + 10)<=21
      suma = sumaTotal + 10
   else
      suma = sumaTotal
   end
end

/* ESTRATEGIA DEL JUGADOR */
newcmd cogerCartasJug cartasJug cartaVisCasa respuesta
   contar cartasJug sumaCartas
   
   if cartaVisCasa=1
      /* si es A */
      cartaVisCasa = 11
   else
      /* si son nobles */
      recode cartaVisCasa between 10 13 10 cartaVisCasa
   end
   
   if (sumaCartas >= 04 AND sumaCartas <= 11)
      respuesta = 1
   elseif sumaCartas = 12 AND (cartaVisCasa <= 3 OR cartaVisCasa >= 7)
      respuesta = 1
   elseif (sumaCartas >= 13 AND sumaCartas <= 16) AND cartaVisCasa >= 7
      respuesta = 1
   else
      respuesta = 0
   end
end

newcmd realizarTurnoJugador cartasIniciales cartasVisCasa cartasResultantes
   concat cartasIniciales cartasResultantes
   cogerCartasJug cartasIniciales cartasVisCasa seguirCogiendo
   
   while seguirCogiendo=1
      take deck (1) cartaCogida
      concat cartaCogida cartasResultantes cartasResultantes
      remove deck (1) deck
      cogerCartasJug cartasResultantes cartasVisCasa seguirCogiendo
   end
end


/* ESTRATEGIA DE LA CASA */

newcmd realizarTurnoCasa cartasCasa cartasResultantes
   contar cartasCasa sumaCartas
   concat cartasCasa cartasResultantes
   
   while sumaCartas <= 16
      take deck (1) cortaCogida
      concat cortaCogida cartasResultantes cartasResultantes
      remove deck (1) deck
      contar cartasResultantes sumaCartas
   end
end

/* LIMPIEZA DE LA CONSOLA */
CLEAROUTPUT

/* JUEGO DE BLACKJACK */

numCiclos = 100000
victoriasJugador = 0

repeat numCiclos

   /* Construccion del deck */
   copy 4#1,13 deck
   shuffle deck deck
   
   /* Inicializacion de la ronda */
   cartasJug = ()
   cartasCasa = ()
   cartasVisCasa = ()
   /* 1 = victoria del jugador (caso asumido), 0 = victoria de la casa */
   resultado = 1
   
   print "===== INICIO DE RONDA ====="
   /* Reparticion inicial */
   take deck (1 3) cartasJug
   size cartasJug numCartasJug
   /* cartas del dealer */
   take deck (2 4) cartasCasa
   size cartasCasa numCartasCasa
   take cartasCasa 2,numCartasCasa cartasVisCasa
   /* final de inicio de la ronda */
   remove deck (1 2 3 4) deck
   
   /* Turno del jugador */
   print "===== Jugador Antes: ====="
   print cartasJug
   print deck
   print "===== Jugador Despues: ====="
   realizarTurnoJugador cartasJug cartasVisCasa cartasResultantesJug
   print cartasResultantesJug
   contar cartasResultantesJug sumaJugador
   print sumaJugador
   print deck
   
   /* Turno de la casa */
   print "===== Casa Antes: ====="
   print cartasCasa
   print deck
   print "===== Casa Despues: ====="
   /* si el jugador ya perdió, la casa no se reparte */
   if sumaJugador > 21
      resultado = 0
      copy cartasCasa cartasResultantes
   else
      realizarTurnoCasa cartasCasa cartasResultantes
   end
   print cartasResultantes
   contar cartasResultantes sumaCasa
   print sumaCasa
   print deck

   /* Si la casa no ha ganado aún, se determina el ganador */
   /* la casa gana si no había ganado ya, no se pasa de 21 y tiene mas puntos que el jugador */
   if resultado = 1 AND sumaCasa <= 21 AND sumaCasa >= sumaJugador
      resultado = 0
   end
   print resultado

   /* Si el jugador ganó, se registra su victoria*/
   victoriasJugador = victoriasJugador + resultado

   print ""
   print ""
   print ""
   
end

/* Calculo e impresion de la probabilidad */
print "===== Probabilidad de victoria del jugador ====="
probabilidad = victoriasJugador / numCiclos
print probabilidad


