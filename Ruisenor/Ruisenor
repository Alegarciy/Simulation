/*
Tecnológico de Costa Rica, Escuela de Computación, Ingeniería en Computación
Curso de Simulación, I Semestre 2021
Tarea del Ruiseñor
Joseph Tenorio Pereira (2019064588) y Alejandro García Bejarano (2019380047) 

Probabilidad de alcanzar $2500 empezando con $2000 y apostando $100, con estrategia clásica: 0.198

Probabilidad de alcanzar $2500 empezando con $2000 y apostando $100, con estrategia clásica: 0.603

*/

/*-----------------------------------------Código Fuente de la simulación-----------------------------------------*/

/* JUEGO DE BLACKJACK */
newcmd jugarBJ res
	copy 42#1 58#0 bolsa
	sample 1 bolsa res
end

/* ESTRATEGIA CLASICA */
newcmd EstrategiaClasica dineroInicio apuestaInicial gananciaDeseada respuesta
	dineroAcumulado = dineroInicio
	
	while (dineroAcumulado > 0 AND dineroAcumulado < gananciaDeseada)
		jugarBJ partida
		if partida=1
			dineroAcumulado = dineroAcumulado + apuestaInicial
		else
			dineroAcumulado = dineroAcumulado - apuestaInicial
		end
	end

	if(dineroAcumulado <= 0)
		respuesta = 0
	else
		respuesta = 1
	end
end

/* ESTRATEGIA DEL RUISEÑOR */
newcmd EstrategiaNightingale dineroInicio apuestaInicial gananciaDeseada respuesta
	dineroAcumulado = dineroInicio
	apuestaEstrategica = apuestaInicial

	while (dineroAcumulado > 0 AND dineroAcumulado < gananciaDeseada)
		jugarBJ partida
		if partida=1
			dineroAcumulado = dineroAcumulado + apuestaEstrategica
			apuestaEstrategica = apuestaInicial
		else 
			dineroAcumulado = dineroAcumulado - apuestaEstrategica
			if (dineroAcumulado < apuestaEstrategica * 2)
				apuestaEstrategica = dineroAcumulado	
			else
				apuestaEstrategica = apuestaEstrategica * 2
			end
		end
	end
	
	if(dineroAcumulado <= 0)
		respuesta = 0
	else 
		respuesta = 1
	end
end 

/* LIMPIEZA DE LA CONSOLA */
CLEAROUTPUT

/* SIMULACION */
victoriasNightingale = 0
victoriasClasica = 0
numCiclos = 1000000

repeat numCiclos

	/* Rondas con estrategia del clasica */	
	EstrategiaClasica 2000 100 2500 resClasica
	victoriasClasica = victoriasClasica + resClasica

	/* Rondas con estrategia clasica */
	EstrategiaNightingale 2000 100 2500 resNightingale
	victoriasNightingale = victoriasNightingale + resNightingale

end 

/* Calculo e impresion de la probabilidad */

probNightingale = victoriasNightingale / numCiclos
probClasica =  victoriasClasica / numCiclos

print "===== Probabilidad de llegar a $2500 con la estrategia clásica ====="
print probClasica
print 
print "===== Probabilidad de llegar a $2500 con la estrategia del ruiseñor ====="
print probNightingale




