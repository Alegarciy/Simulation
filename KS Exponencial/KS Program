  /*
Tecnologico de Costa Rica, Escuela de Computacion, Ingenieria en Computacion
Curso de Simulacion, I Semestre 2021
Tarea de KS
Joseph Tenorio Pereira (2019064588) y Alejandro Garcia Bejarano (2019380047) 

Deseamos construir un programa que realiza una prueba de Kolmogorov-Smirnov sobre un conjunto de datos que se presume
vienen de una distribución exponencial.
Dado el archivo de prueba y un tamaño de intervalo 2, el programa obtiene como valor d = 0.009481
Dado el archivo de prueba y un tamaño de intervalo 3, el programa obtiene como valor d = 0.010134
*/

/*-----------------------------------------Codigo Fuente de la simulacion-----------------------------------------*/
CLEAROUTPUT
/* SUBRUTINAS */

/* 
Lee el archivo de datos y el tamaño de los intervalos, calcula el promedio, la desviación y el tamaño de muestra
*/
NEWCMD leerDatos _datos _tamañoIntervalo _tamañoMuestra
	READ _datos
	INPUT _tamañoIntervalo
	SIZE _datos _tamañoMuestra
END 

/* 
Determina los límites de los intervalos a utilizar
*/
NEWCMD calcularLimites datos tamañoIntervalo _limitesInferiores _limitesSuperiores
	MIN datos minimo
	MAX datos maximo
	limInferior = 0
	/* Se calcula el límite inferior más bajo cuyo intervalo tenga frecuencia observada distinta de cero */
	WHILE minimo - limInferior >= tamañoIntervalo
		limInferior = limInferior + tamañoIntervalo
	END
	limSuperior = limInferior + tamañoIntervalo
	/* Se calcula el límite superior más alto cuyo intervalo tenga frecuencia observada distinta de cero */
	WHILE limSuperior < maximo
		CONCAT _limitesInferiores limInferior _limitesInferiores
		CONCAT _limitesSuperiores limSuperior _limitesSuperiores
		limInferior = limInferior + tamañoIntervalo
		limSuperior = limInferior + tamañoIntervalo
	END
	CONCAT _limitesInferiores limInferior _limitesInferiores
	CONCAT _limitesSuperiores limSuperior _limitesSuperiores
END

/* 
Calcula las frecuencias observadas de cada intervalo
*/
NEWCMD calcularFrecObs datos limitesSuperiores _frecuenciasObs
	SIZE limitesSuperiores cantidadInvervalos
	COPY cantidadInvervalos#0 _frecuenciasObs
	FOREACH dato datos
		numeroIntervalo = 1
		TAKE limitesSuperiores numeroIntervalo limiteSup
		/* Se determina el intervalo de los datos comparandolos con limites superiores ascendentementen */
		WHILE dato > limiteSup
			numeroIntervalo = numeroIntervalo + 1
			TAKE limitesSuperiores numeroIntervalo limiteSup
		END
		numeroIntervalo = numeroIntervalo - 1
		COPY numeroIntervalo#0 1 0 posicion
		ADD _frecuenciasObs posicion _frecuenciasObs 
	END
	/* Remueve el cero sobrante al final de la lista de Fo, como resultado de la cuenta de las frecuencias */
	sobrante = cantidadInvervalos + 1
	REMOVE _frecuenciasObs sobrante _frecuenciasObs
END

/* 
Calcula la frecuencia y probabilidad acumulada observados de cada intervalo 
*/
NEWCMD calcularFrecAcum frecuenciasObs _frecuenciaAcum _probabilidadAcum
	acum = 0
	pacum = 0
	SUM frecuenciasObs sumatoria
	FOREACH frecuencia frecuenciasObs
		acum = acum + frecuencia
		pacum = acum / sumatoria 
		CONCAT _frecuenciaAcum acum _frecuenciaAcum
		CONCAT _probabilidadAcum pacum _probabilidadAcum
	END
END

/* 
Calcula el valor Beta de la distribución y la probabilidad acumulada esperada de cada intervalo
*/
NEWCMD calcularProbAcumEsperada datos limitesSup _probabilidadAcumEsp
	MEAN datos beta
	_probabilidadAcumEsp = 1 - 2.718281828 ^ ( - limitesSup / beta )
END

/* 
Calcula la diferencia absoluta entre poa y pea de cada intervalo y el valor D de la prueba
*/
NEWCMD calcularD probabilidadAcumObs probabilidadAcumEsp _diferenciaProbsAcums _valorD
	_diferenciaProbsAcums = probabilidadAcumObs - probabilidadAcumEsp
	ABS _diferenciaProbsAcums _diferenciaProbsAcums
	MAX _diferenciaProbsAcums _valorD
END


/* MAIN */
leerDatos datos tamañoIntervalo tamañoMuestra
calcularLimites datos tamañoIntervalo limitesInf limitesSup
calcularFrecObs datos limitesSup frecObs
calcularFrecAcum frecObs frecAcum probObsAcum
calcularProbAcumEsperada datos limitesSup probEspAcum
calcularD probObsAcum probEspAcum diferencia valorD
PRINT
PRINT tamañoMuestra
PRINT
PRINT TABLE limitesInf limitesSup frecObs frecAcum probObsAcum probEspAcum diferencia
PRINT
PRINT valorD





