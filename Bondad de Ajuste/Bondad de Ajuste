/*
Tecnologico de Costa Rica, Escuela de Computacion, Ingenieria en Computacion
Curso de Simulacion, I Semestre 2021
Tarea de Pruebas de Series
Joseph Tenorio Pereira (2019064588) y Alejandro Garcia Bejarano (2019380047) 

Deseamos construir un programa que realiza un ajuste de chi cuadrado sobre un conjunto de datos que se presume
vienen de una distribución normal.

Valor chi cuadrado obtenido con los datos de muestra e intervalos de tamaño 1: 33.283732
Valor chi cuadrado obtenido con los datos de muestra e intervalos de tamaño 2: 16.420984
*/

/*-----------------------------------------Codigo Fuente de la simulacion-----------------------------------------*/

CLEAROUTPUT

/* SUBRUTINAS */

/* 
Lee el archivo de datos y el tamaño de los intervalos, calcula el promedio, la desviación y el tamaño de muestra
*/
NEWCMD leerDatos _datos _promedio _desviacion _tamañoIntervalo _tamañoMuestra
	READ _datos
	MEAN _datos _promedio
	STDEV _datos _desviacion
	INPUT _tamañoIntervalo
	SIZE _datos _tamañoMuestra
END 

/* 
Determina los límites de los intervalos a utilizar
*/
NEWCMD calcularLimites datos tamañoIntervalo _limitesInferiores _limitesSuperiores
	MIN datos minimo
	MAX datos maximo
	limInferior = 0
	/* Se calcula el límite inferior más bajo cuyo intervalo tenga frecuencia observada distinta de cero */
	WHILE minimo - limInferior >= tamañoIntervalo
		limInferior = limInferior + tamañoIntervalo
	END
	limSuperior = limInferior + tamañoIntervalo
	/* Se calcula el límite superior más alto cuyo intervalo tenga frecuencia observada distinta de cero */
	WHILE limSuperior < maximo
		CONCAT _limitesInferiores limInferior _limitesInferiores
		CONCAT _limitesSuperiores limSuperior _limitesSuperiores
		limInferior = limInferior + tamañoIntervalo
		limSuperior = limInferior + tamañoIntervalo
	END
	CONCAT _limitesInferiores limInferior _limitesInferiores
	CONCAT _limitesSuperiores limSuperior _limitesSuperiores
END

/* 
Calcula las frecuencias observadas de cada intervalo
*/
NEWCMD calcularFrecObs datos limitesSuperiores _frecuenciasObs
	SIZE limitesSuperiores cantidadInvervalos
	COPY cantidadInvervalos#0 _frecuenciasObs
	FOREACH dato datos
		numeroIntervalo = 1
		TAKE limitesSuperiores numeroIntervalo limiteSup
		/* Se determina el intervalo de los datos comparandolos con limites superiores ascendentementen */
		WHILE dato > limiteSup
			numeroIntervalo = numeroIntervalo + 1
			TAKE limitesSuperiores numeroIntervalo limiteSup
		END
		numeroIntervalo = numeroIntervalo - 1
		COPY numeroIntervalo#0 1 0 posicion
		ADD _frecuenciasObs posicion _frecuenciasObs 
	END
	/* Remueve el cero sobrante al final de la lista de Fo, como resultado de la cuenta de las frecuencias */
	sobrante = cantidadInvervalos + 1
	REMOVE _frecuenciasObs sobrante _frecuenciasObs
END

/* 
Determina la probabilidad acumilada de un intervalo 
*/
newcmd probNormalRango limInf limSup promedio desviación prob
       normalprob limInf promedio desviación probInf
       normalprob limSup promedio desviación probSup
       prob = probSup - probInf
end

/* 
Calcula la probabilidad esperada y frecuencia esperada 
*/
newcmd probabilidadEsperada limitesInf limitesSup promedio desviacion datos _frecuenciasObservadas _probabilidadesEsperadas _frecuenciasEsperadas
	SIZE limitesSup cantidadInvervalos
	SIZE datos cantidadDatos
	numeroIntervalo = 1
	borrados = 0
	
	WHILE cantidadInvervalos >= numeroIntervalo
		TAKE limitesSup numeroIntervalo limiteSup 
		TAKE limitesInf numeroIntervalo limiteInf
		probNormalRango limiteInf limiteSup promedio desviacion probAcumulada
		frecuenciaEsperada = probAcumulada * cantidadDatos

		IF frecuenciaEsperada > 5
			CONCAT _frecuenciasEsperadas frecuenciaEsperada _frecuenciasEsperadas
			CONCAT _probabilidadesEsperadas probAcumulada _probabilidadesEsperadas
		ELSE
			numeroIntervaloBorrar = numeroIntervalo - borrados
			REMOVE _frecuenciasObservadas  numeroIntervaloBorrar _frecuenciasObservadas
			borrados = borrados + 1
		END
		/*
		CONCAT _frecuenciasEsperadas frecuenciaEsperada _frecuenciasEsperadas
		CONCAT _probabilidadesEsperadas probAcumulada _probabilidadesEsperadas */
		numeroIntervalo = numeroIntervalo + 1
	END
END

/* 
Calcula el valor de chi cuadrado subcero
*/
NEWCMD getChiSquare Fo Fe chiSquareResult
chisquare Fo Fe chiSquareResult
END


/* MAIN */
leerDatos datos promedio desviacion tamañoIntervalo tamañoMuestra
calcularLimites datos tamañoIntervalo limitesInf limitesSup
calcularFrecObs datos limitesSup frecuenciasObs
PRINT
PRINT tamañoMuestra
PRINT
PRINT promedio
PRINT desviacion
PRINT
PRINT TABLE limitesInf limitesSup frecuenciasObs
PRINT
probabilidadEsperada limitesInf limitesSup promedio desviacion datos frecuenciasObs probabilidadEsp fecuenciaEsp
getChiSquare frecuenciasObs fecuenciaEsp chiCuad
PRINT TABLE probabilidadEsp fecuenciaEsp frecuenciasObs
PRINT
PRINT chiCuad
