CLEAROUTPUT

NEWCMD getCantidadPedidos pedidos
	POISSON 1 7 pedidos
END

NEWCMD laborarDia cantidadUnidadesInventarioI hayPedido diasEntrega cantidadPorOrdenar _hayPedido _diasEntrega _cantidadPedidos _cantidadUnidadesInventarioF
	
	/* Generar Peidios */
	getCantidadPedidos pedidos
	_cantidadPedidos = pedidos

	/* Asignacion de variables */
	_cantidadUnidadesInventarioI = cantidadUnidadesInventarioI
	_cantidadUnidadesInventarioF = cantidadUnidadesInventarioI
	_hayPedido = hayPedido

	/* Revisar si se necesita hacer re-fill */
	IF diasEntrega > 1 
		_diasEntrega = diasEntrega - 1 
	ELSE
		IF diasEntrega = 1
			 _diasEntrega = diasEntrega - 1 
			 _cantidadUnidadesInventarioF = _cantidadUnidadesInventarioF + cantidadPorOrdenar
			 _hayPedido = 0
		ELSE
			_diasEntrega = diasEntrega
		END
	END
	
	/* Substraer los pedidos entegados del dia */
	_cantidadUnidadesInventarioF = _cantidadUnidadesInventarioF - _cantidadPedidos
END

NEWCMD generarDiasEntrega resultado
	UNIFORM 1 0 1 random
	resultado = 0
	IF random <= 0.3 
		resultado = 1
	END
	IF 0.3 < random AND random <= 0.7
		resultado = 2
	END
	IF 0.7 <= random
		resultado = 3
	END
END


NEWCMD simularLabor corridas cantidadUnidadesInventarioIniciales cantidadPorOrdenar puntoDeReorden No ini q t x fin
	
	/* Estados Iniciales */
	hayPedido = 0
	diasEntrega = 0
	cantidadPedidos = 0
	corridasSimulacion = 0
	cantidadFinalDelDia = cantidadUnidadesInventarioIniciales
	cantidadInicioDelDia = cantidadUnidadesInventarioIniciales
	
	/* Mostrar Resultados */
	No = ()
	ini = ()
	q = ()
	T = ()
	x = ()
	fin = ()

	/* Inicio de Simulacion */
	WHILE corridasSimulacion < corridas 

		cantidadInicioDelDia = cantidadFinalDelDia
		
		/* Pedir Unidades */
		IF hayPedido = 0 AND cantidadFinalDelDia <= puntoDeReorden
			generarDiasEntrega resultadoDias
			diasEntrega = resultadoDias
			hayPedido = 1
		END
		
		laborarDia cantidadInicioDelDia hayPedido diasEntrega cantidadPorOrdenar _hayPedido _diasEntrega _cantidadPedidos _cantidadUnidadesInventarioF
		
		/* Actualizar datos de dia laborado */
		cantidadFinalDelDia = _cantidadUnidadesInventarioF 
		hayPedido = _hayPedido
		diasEntrega = _diasEntrega
		cantidadPedidos = _cantidadPedidos
		
		/*
		CONCAT corridasSimulacion cantidadInicioDelDia hayPedido diasEntrega cantidadPedidos cantidadFinalDelDia _datosDia
		*/

		CONCAT No corridasSimulacion No
		CONCAT ini cantidadInicioDelDia ini
		CONCAT q hayPedido q
		CONCAT t diasEntrega t
		CONCAT x cantidadPedidos x
		CONCAT fin cantidadFinalDelDia fin

		corridasSimulacion = corridasSimulacion + 1
	END
END 

simularLabor 100 50 50 20 No ini q t x fin
print TABLE No%8.0F ini%8.0F q%8.0F t%8.0F x%8.0F fin%8.0F



